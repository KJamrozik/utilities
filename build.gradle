group 'com.github.konrad-jamrozik'
version 'dev'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'

compileJava.sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.targetCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenCentral()
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'groovydoc'
  from groovydoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives groovydocJar
}

dependencies {

  compile 'org.codehaus.groovy:groovy-all:2.4.5'
  compile 'commons-io:commons-io:2.4'
  compile 'com.google.jimfs:jimfs:1.0'

  testCompile group: 'junit', name: 'junit', version: '4.12'

  File resDir = sourceSets.test.resources.srcDirs.first()
  File subdir = new File(resDir, "subdir")
  File fixtureZip = resDir.listFiles().find { it.name == "_fixture.zip" }
  testRuntime files(fixtureZip)
  testRuntime files(subdir)
}

task debug {
  doLast {
    // Reference: https://discuss.gradle.org/t/how-to-get-a-complete-list-of-all-artifacts-generated-by-a-build/4492
//    println "Archives Artifacts: " + configurations.archives.allArtifacts
//    println project.buildDir
    sourceSets.test.runtimeClasspath.each { println it }
  }
}

tasks.withType(Test) {
  testLogging {
    events 'started', 'passed'
    showStandardStreams = true
  }
}

task testi(type: Test, dependsOn: testClasses) {
  outputs.upToDateWhen {false}
  include '**/*ResourcePathTest*'
}